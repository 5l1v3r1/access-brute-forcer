apply plugin: 'com.android.application'

// See https://developer.android.com/studio/publish/app-signing.html#secure-key
def releaseSignKeystoreProperties = new Properties()
def keystoreFileName = "keystore.properties"
// Initialize the properties placeholders if the keystore information file exists
if(rootProject.file(keystoreFileName).exists()){
    releaseSignKeystoreProperties.load(new FileInputStream(rootProject.file(keystoreFileName)))
}else{
    //Otherwise set dummy values to make the build file and context valid
    releaseSignKeystoreProperties['keyAlias'] = "dummy"
    releaseSignKeystoreProperties['keyPassword'] = "dummy"
    releaseSignKeystoreProperties['storeFile'] = "../dummy.jks"
    releaseSignKeystoreProperties['storePassword'] = "dummy"
}



android {
    signingConfigs {
        releaseConfig {
            keyAlias releaseSignKeystoreProperties['keyAlias']
            keyPassword releaseSignKeystoreProperties['keyPassword']
            storeFile file(releaseSignKeystoreProperties['storeFile'])
            storePassword releaseSignKeystoreProperties['storePassword']
        }
    }
    compileSdkVersion 25
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "righettod.eu.accessbf"
        minSdkVersion 24
        targetSdkVersion 25
        versionCode 1
        versionName "1.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [HOCKEYAPP_APP_ID: "64dd8a3981644cfd9923617dc0d05989"]
        signingConfig signingConfigs.releaseConfig
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            signingConfig signingConfigs.releaseConfig
            renderscriptDebuggable false
            zipAlignEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:support-v13:25.4.0"
    implementation 'org.apache.commons:commons-compress:1.14'
    implementation 'org.samba.jcifs:jcifs:1.3.3'
    implementation 'com.jcraft:jsch:0.1.54'
    implementation 'commons-net:commons-net:3.6'
    implementation 'com.android.support:support-core-utils:25.4.0'
    implementation 'net.hockeyapp.android:HockeySDK:5.0.0'
    testImplementation 'junit:junit:4.12'
}
